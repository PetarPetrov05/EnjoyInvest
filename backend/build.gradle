plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.flywaydb.flyway' version '9.22.0'
    id 'org.sonarqube' version '7.0.1.6134'
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    runtimeOnly 'org.postgresql:postgresql'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-gson:0.11.5'

    // Flyway
    implementation 'org.flywaydb:flyway-core:9.22.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    testCompileOnly 'org.projectlombok:lombok:1.18.32'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'

    // Test libs
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'com.h2database:h2'
}

// Flyway config
flyway {
    url = 'jdbc:postgresql://localhost:5432/enjoyinvest'
    user = 'postgres'
    password = 'postgres'
    schemas = ['public']
    locations = ['filesystem:src/main/resources/db/migrations']
}

// Jacoco config
jacoco {
    toolVersion = "0.8.11" // updated for Java 21 support
}

// Ensure tests use JUnit 5 and generate Jacoco report
tasks.named('test') {
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'test'
    finalizedBy tasks.jacocoTestReport
}

// Jacoco report task
tasks.jacocoTestReport {
    dependsOn tasks.test
    reports {
        xml.required = true      // Required for SonarQube
        html.required = true     // Visual report
        csv.required = false
    }
    // Ensure it uses correct execution data file
    executionData.setFrom(fileTree(buildDir).include("jacoco/test.exec"))
}

// SonarQube config
sonarqube {
    properties {
        property "sonar.projectKey", "PetarPetrov05_EnjoyInvest"
        property "sonar.organization", "petarpetrov05"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.binaries", "$buildDir/classes/java/main"
        property "sonar.junit.reportPaths", "$buildDir/test-results/test"
        property "sonar.coverage.jacoco.xmlReportPaths", "$buildDir/reports/jacoco/test/jacocoTestReport.xml"
    }
}

// Java compiler options
tasks.withType(JavaCompile) {
    options.annotationProcessorPath = configurations.annotationProcessor
    options.compilerArgs += ["-parameters"]
}
